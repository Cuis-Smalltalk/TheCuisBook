@node Visual with Morph
@chapter Visual with Morph

Every object you see in @cuis{} is a @class{Morph} or is composed
of @class{Morph}s.  Basically, a @class{Morph} is an object with
state and behavior that can also depict itself on a computer
display screen.

Because Morphs are useful, when you look at
class @class{Morph} in a Hierarchy Browser you will
see a large number of methods and many, many subclasses.
But the basic ideas are quite simple.


@menu
* Ellipse Morph::
* Submorph::
* A brief introduction to Inspectors::
* Building your specialized Morph::
* Spacewar! graphics::
@end menu

@node Ellipse Morph
@section Ellipse Morph
@cindex morph, ellipse
Let's start with one of the basic morphs, an @class{EllipseMorph}.
You could write @smalltalk{EllipseMorph new openInWorld} and
@emph{@b{D}o-it}, but we are doing visual things for now, so let's get
a World Menu and select from @label{New Morph...} @label{Basic}
submenu and drag onto the desktop.

Every time one obtains a morph from a @strong{New Morph...} submenu,
one gets a different morph but made to a standard style.

@figure{Select @class{EllipseMorph} from a
Menu,ch06-01-EllipseMorphFromMenu,8}

The basic challenge of @dfn{user interface design} is
to communicate visibility and control.
Where am I?  What can I do here?

One of the balance points in design is how to eliminate clutter.  One
useful strategy is to reveal capabilities in context as they are
needed.

@cindex morph, halo
In the case of Cuis, you have to know some basics because helpful
tools are there but stay out of the way.  At any time you can
@kbd{Right-Click} on the desktop to get the World Menu.  You can also
@kbd{Middle-Click} on any @class{Morph} to get a @dfn{halo} of
@dfn{construction handles}, which show up as small colored circular
icons.  If you pause the cursor over one of these, you get a @dfn{tool
tip}, a temporary text popup who's name should give a clue to its
usage.

If you click elsewhere the construction handles leave, you get them
back at any time with a mouse click.

@figure{Drag Construction Handle to Change Size,ch06-03-DragToEnlarge,4}

Now that you know this, move the yellow lower right handle with tool
tip @strong{Change size} via @dfn{Click-Drag}.  Just hold down the
left mouse button while the cursor is over the handle, move the cursor
to the right and down, and release the mouse button.

@figure{A Larger Ellipse,ch06-04-Enlarged,4}

@node Submorph
@section Submorph
@cindex morph, submorph
@cindex morph, rectangle
Morphs can contain other morphs.  These interior morphs are called
@dfn{submorph}s of their containing morph. Again, you can do this by
writing the software ``code'', but let's do it directly with a
@class{WidgetMorph}.

First we obtain a WidgetMorph from the @strong{New morph...} submenus.

@figure{Obtain a WidgetMorph,ch06-05-AddRectangle,5}

Now drag the rect over the ellipse and @kbd{Middle-Click} on the rect
and click on the blue construction handle to gets the rect's
@dfn{Morph Menu}.  Use the menu selection @strong{embed into...} and
select the ellipse as its new parent.

@figure{Make the Rect a Submorph of the
Ellipse,ch06-06-EmbedRectIntoEllipse,5}

Now when you click-drag the ellipse, or use the @strong{Pick up} or
@strong{Move} construction handles, the rect is just a decoration for
the ellipse.

Indeed, the rect seems to have fused into the ellipse.  Using the
mouse where the rect shows itself is just using the mouse on the
ellipse.  This rect does not have many interesting behaviors.

Let's add a behavior to just this one @class{WidgetMorph}.

@node A brief introduction to Inspectors
@section A brief introduction to Inspectors
To get the construction halo for an interior morph, just
@kbd{Middle-Click} multiple times to ``drill down'' through the
submorph hierarchy.

@figure{Middle-Click for Construction Handles,ch06-07-MiddleClickRect,4.5}

@figure{Middle-Click again to descend into
Submorphs,ch06-08-2ndMidClckToSubmorph,4.5}

There is an orange handle on the right, just under the green
@strong{Duplicate} handle.  @kbd{Left-Click} this to get the
@strong{Debug} menu.  Use this menu to get an @dfn{Inspector} for the
rectangle.

@cindex tools, inspector
Observe @ref{ch06-11aColorClickOnRect}, on the left we have a pane for
self, all inst vars, and the individual instance variables.  Clicking
to select ``all inst vars'' and the values pane on the right shows
that the owner of the rect is the ellipse and rect currently has no
submorphs.

The lower pane is a Smalltalk code editor, basically a workspace,
where @strong{self} is bound to the object we are inspecting.

Inspectors work for every object by the way, not just morphs.

@figure{Add Instance Specific Behavior,ch06-11aColorClickOnRect,8}

To add a behavior to all instances of a class, we create an instance
method.  Here we are going to create a behavior for ``just this one
WidgetMorph instance''.

@anchor{morphProperties}
@cindex morph, properties
In addition to instance variables, a morph can have any number of
named @dfn{properties} which can be different for each morph.

We add two properties here:

@smalltalkExample{self setProperty: #handlesMouseDown: toValue: true.
self setProperty: #mouseButton1UpLocalPosition:
     toValue: [:ignoredEvent :ignoredPosition| self color: Color random]
}

These properties are special to the user interface.  You can find
methods with these names in the @class{Morph} class to see what they
do.

After selecting the text and @emph{@b{D}o-it}, each time you
@kbd{Left-Click} on the rect it changes color!

Note that you can no longer move the ellipse by mouse-down on the
rect, because the rect now takes the mouse event.  You have to
mouse-down on the Ellipse.  More on this below.

@cindex morph, move/pick up
One quick note on @strong{Move} versus @strong{Pick up}.
@strong{Move} moves a submorph ``within'' its parent.  @strong{Pick
up} grabs a morph ``out'' of its parent.

@figure{Move Submorph within its Parent,ch06-09-Move-Within,4}

@figure{Pick a Submorph out of its Parent,ch06-10-PickUp-2-MoveOut,4}

@page
Before we go on, let's use an inspector on the ellipse to change
values of a couple of its instance variables.

@figure{Inspect instance variables of the Ellipse,ch06-12-InspectEllipse,7}

Observe @ref{ch06-14-SetBorderColor}. In the lower pane of the
inspector, code can be executed in the context of the inspected
object. @smalltalk{self} refers to the instance. Here the pane
contains code to set the borderWidth and the borderColor.

@figure{Use Inspector to set borderColor and
borderWidth,ch06-14-SetBorderColor,7}

In the typical case one wants to refine or change behaviors for all
instances of a class.

@node Building your specialized Morph
@section Building your specialized Morph
@cindex morph, subclass
Let's make a simple subclass which changes color when
@kbd{Left-Click}ed.  Create a new class just as we did with Spacewar!
but subclass EllipseMorph with #ColorClickEllipse.

@smalltalkExample{EllipseMorph subclass: #ColorClickEllipse
             instanceVariableNames: ''
             classVariableNames: ''
             poolDictionaries: ''
             category: 'Spacewar!!'}

@kbd{Right-Click} on the @strong{Message Category} pane and select
@strong{new category...}.  This brings up a number of selections and
allows us to create new ones.  Select ``event handling testing''.
Then add the method @smalltalk{ColorClickEllipse>>handlesMouseDown:}.

@smalltalkExample{handlesMouseDown: aMouseButtonEvent
  "Answer that I do handle mouseDown events''
  ^true
}

Likewise, add a new catagory ``event handing'' and add the other
method we need.

@smalltalkExample{mouseButton1Up: aMouseButtonEvent localPosition: localEventPosition
   "I ignore the mouseEvent information and change my color."
   self color: Color random}

Now, you have created a new Morph class and can select a
ColorClickEllipse from the World Menu @strong{New Morph..} and try it
out.  These are fun to @kbd{Left-Click} on.  Make as many as you want!

@figure{Obtain ColorClickEllipse,ch06-15-ColorClickEllipse,8}

Now you know how to specialize an individual morph, or make a whole
new class of them!


@node Spacewar! graphics
@section Spacewar! graphics

@subsection All Morphs
Previously we defined the actors of the game as subclasses of the very
general @class{Object} class (See
@ref{torpedoClassVariables}). However the game play, the central star,
the ships and the torpedoes are visual objects, each with a dedicated
graphic shape:

@itemize

  @item the game play is a simple rectangular area filled with the
  black color,

  @item the central star is a fluctuating yellow disk with an orange
  aura,

  @item the ships are rotating quadrangles each one painted with a
  different color,

  @item a torpedo is a rotating triangle to paint with a different
  color depending on the firing ship.
  
@end itemize

Therefore it makes sense to turn these actors as kind of
@class{Morph}s, the visual entity of @cuis{}. To do so, point a System
Browser to the class definition of each actor, replace the parent
class @class{Object} by @class{Morph}, then save the class definition
with @kbd{Ctrl-s}.

For example, the torpedo class as seen in @ref{torpedoClassVariables}
is edited as:

@smalltalkExample{Morph subclass: #Torpedo
   instanceVariableNames: 'position velocity lifeSpan'
   classVariableNames: ''
   poolDictionaries: ''
   category: 'Spacewar!'}

Moreover, as you may have guessed, a Morph already knows about its
position on screen -- it can be dragged in the screen with the mouse
cursor. Therefore the @smalltalk{position} instance variable is
redundant and should be removed. For now we keep it, it will be
removed later when we will know how to replace each of its use case
with its appropriate Morph counter part.

@exercise{Make all Morphs, allActorsMorph, @emph{Edit
@smalltalk{SpaceWar}@comma{} @class{CentralStar} and @class{SpaceShip}
as subclasses of the @class{Morph} class.}}

As explained in the previous sections of this chapter, a morph can be
embedded in another game. In Spacewar!, a @class{SpaceWar} morph
instance representing the game play, it is the @emph{owner} of the central
star, space ship and torpedo morphs. Put in other words, the central
star, space ships and torpedoes are @emph{sub morphs} of a
@class{SpaceWar} morph instance.

The @method{SpaceWar>>initializeActors} code in @ref{initActors} is
 not complete without adding and positioning the central star and
 space ships as sub morphs of the Spacewar! game play:

@smalltalkExampleCaption{Complete code to initialize the Spacewar! actors, initFullActors,
SpaceWar>>initializeActors
   centralStar @assign{} CentralStar new.
   self addMorph: centralStar.
   centralStar morphPosition: 0 @@ 0.
   ships @assign{} Array 
      with: (SpaceShip new color: Color white)
      with: (SpaceShip new color: Color red).
   self addAllMorphs: ships.
   ships first morphPosition: 200 @@ -200.
   ships second morphPosition: -200 @@ 200.
   torpedoes @assign OrderedCollection new}

There are two important messages: @msg{addMorph:} and
@msg{morphPosition:}. The former asks to the receiver morph to embed
its morph argument as a sub morph, the later asks to set the receiver
coordinates in its owner reference frame. From reading the code, you
deduce the origin of the owner reference frame is its middle,
indeed our central star is in the middle of the game play.

There is a third message not written here, @msg{morphPosition}, to ask
the coordinates of the receiver in its owner reference frame.

Remember our discussion about the @smalltalk{position} instance
variable. Now you clearly understand it is redundant and we remove it
from the @class{SpaceShip} and @class{Torpedo} definitions. Each time
we need to access the position, we just write @smalltalk{self
morphPosition} and each time we need to modify the position we write
@smalltalk{self morphPosition: newPosition}. More on that later.


@c refactor ship and torpedo as mobile entity
@cindex refactoring
@subsection The art of refactoring
In our @ref{newtonModel,, newtonian model} we explained the
space ships are subjected to the engine acceleration and the gravity
pull of the central star. The equations are described in
@ref{motionEquations}.

Based on these mathematics, we wrote the @smalltalk{SpaceShip>>update:}
method to update the ship position according to the elapsed time --
see @ref{spaceShipMechanic}.

So far in our model, a torpedo is not subjected to the central start
gravity pull nor its engine acceleration. It is supposing its mass is
zero which is unlikely. Of course the @smalltalk{Torpedo>>update:}
method is simpler than the space ship counter part -- see
@ref{torpedoMechanic}. Nevertheless, it is more accurate and even more
fun that the torpedoes are subjected to the gravity pull@footnote{So a
torpedo should come with a mass.} and its engine acceleration; an
agile space ship pilot could use gravity assist to accelerate a
torpedo fired with a path close to the central star.

@todo{It will be nice to have a dotted track of a torpedo using
gravity assist.}

What are the impacts of these considerations on the torpedo and space
ship entities?

Firstly, they will share @emph{common states} as the mass, the
position, the velocity and the acceleration.

Secondly, they will share @emph{common behaviors} as the computation
to update the position and velocity.

Thirdly, they will have @emph{different states}: a torpedo has a life
span state while a space ship has fuel tank capacity and torpedoes
stock states.

Fourthly, they will have @emph{different behaviors}: a torpedo is self
destroyed when its life span expires, a space ship fires torpedoes and
accelerates as long as its fuel tank and its torpedoes count are not
zero.

Sharing states and behaviors along different states and behaviors are
a strong indication that the @class{SpaceShip} and @class{Torpedo}
classes should share a common ancestor class; one more specialized
than the @class{Morph} class they currently share.

Doing such analysis on the computer model of the game is part of the
@dfn{refactoring} effort to avoid behavior and state duplications while
making more obvious common logic in the entities. The general idea of
code refactoring is to rework existing code to make it more elegant,
understandable and logic.

To do so, we want to introduce a @class{Mobile} class, kind of
 @class{Morph} with behaviors specific to a mobile subjected to
 accelerations. Its states are the mass, position, velocity and
 acceleration. Well, as we are discussing refactoring, the mass state
 does not really makes sense in our game, indeed our mobile's mass is
 constant. So we just need a method returning a literal number and we
 remove the @smalltalk{mobile} instance variable.

It results in this @class{Mobile} definition:

@smalltalkExampleCaption{Mobile in the game play,mobileClass,Morph subclass: #Mobile
   instanceVariableNames: 'velocity acceleration'
   classVariableNames: ''
   poolDictionaries: ''
   category: 'Spacewar!'}

@exercise{Refactoring @class{SpaceShip} and @class{Torpedo},refactoryShipTorpedo,
@emph{What should be the refactored definitions of the
@class{SpaceShip} and @class{Torpedo} classes?}}

The first behaviors we add to our @class{Mobile} are its
initialization and its mass:

@smalltalkExample{Mobile>>initialize
  super initialize.
  velocity @assign{} 0 @@ 0.
  acceleration @assign{} 0
        
Mobile>>mass
  ^ 1}

The next methods to add are the ones relative to the mechanics
calculus. First, the gravity acceleration calculus:

@smalltalkExampleCaption{Calculate the gravity force,gravityForce,
Mobile>>gravity
"Compute the gravity acceleration vector"
   | position |
   position @assign{} self morphPosition.
   @return{} -10 * self mass * owner starMass / (position r raisedTo: 3) * position}

It deserves a few comments:
@itemize

  @item @smalltalk{self morphPosition} returns a @class{Point}
  instance, the position of the mobile in the owner reference frame,

  @item @smalltalk{owner} is the @class{SpaceWar} instance
  representing the game play. It is the owner -- parent morph -- of
  the mobile. When asking @msg{starMass}, it interrogates its central
  star mass and return its value:

  @smalltalkExample{SpaceWar>>starMass
   @return{} centralStar mass}

  @item In @smalltalk{position r}, the @msg{r} message asks the radius
  attribute of a point considered in polar coordinates. It is just its
  length, norm. It is the distance between the mobile and the
  central star.

  @item @smalltalk{* position} really means multiply the previous
  scalar value with a @class{Point}, hence a vector. Thus the returned
  value is a @class{Point}, a vector in this context, the gravity
  vector.

@end itemize

The method to update the mobile position and velocity is mostly the
same as in @ref{spaceShipMechanic}. Of course the
@smalltalk{SpaceShip>>update:} and @smalltalk{Torpedo>>update:}
version must be both deleted. Below the complete version with the
morph way to access the mobile's position:

@smalltalkExampleCaption{@class{Mobile}'s @method{update:} method,mobileUpdate,
Mobile>>update: t
"Update the mobile position and velocity"
  | ai ag newVelocity |
  "acceleration vectors"
  ai @assign{} acceleration * self direction.
  ag @assign{} self gravity.
  newVelocity @assign{} (ai + ag) * t + velocity.
  self morphPosition:
     (0.5 * (ai + ag) * t squared)
     + (velocity * t)
     + self morphPosition.
  velocity @assign{} newVelocity.	
  "Are we out of screen? If so we move the mobile to the other corner
  and slow it down by a factor of 2"  
  (self isInOuterSpace and: [self isGoingOuterSpace]) ifTrue: [
     velocity @assign{} velocity / 2.
     self morphPosition: self morphPosition negated]}

Now we should add the two methods to detect when a mobile is heading
to deep space:

@smalltalkExampleCaption{Test when a mobile is going deep space,mobileDeepSpace,
Mobile>>isInOuterSpace
"Is the mobile located in the outer space? (outside of the game
play area)"
   @return{} (owner morphContainsPoint: self morphPosition) not

Mobile>>isGoingOuterSpace
"is the mobile going crazy in the direction of the outer space?"
   @return{} (self morphPosition dotProduct: velocity) > 0}

As you see, these test methods are simple and short. When writing
@cuis{} code, this is something we appreciate a lot and we do not
hesitate to cut a long method in several small methods. It improves
readability and code reuse. The @msg{morphContainsPoint:} message asks
the receiver morph whether the point in argument is inside its shape.

@cindex overriding
When a mobile is updated, its position and velocity are
updated. However the @class{Mobile} subclasses @class{SpaceShip} or
@class{Torpedo} may need additional specific update. In object
oriented programming there is this special mechanism named
@dfn{overriding} to achieve this.

See the @class{Torpedo>>update:} definition:

@smalltalkExample{Torpedo>>update: t
   "Update the torpedo position"
   super update: t.
   "orientate the torpedo in its velocity direction, nicer effect
   while inaccurate"
   self rotation: (velocity y arcTan: velocity x) + Float halfPi scale: 1.
   lifeSpan @assign{} lifeSpan - 1.
   ifeSpan isZero ifTrue: [owner destroyTorpedo: self].
   acceleration > 0 ifTrue: [acceleration @assign{} acceleration - 1000]}

Here the @method{update:} method is specialized to the torpedo
specific needs. The mechanic calculus done in @method{Mobile>>update:}
is still used to update the torpedo position and velocity: this is
what does @smalltalk{super update: t}. We already discussed
@smalltalk{super} previously. In the context of
@method{Torpedo>>update:} it means search for an @method{update:}
method in @class{Torpedo}'s parent class; and so on until the method
is found, if not a @emph{Message Not Understood} error is thrown.

Among the specific added behaviors, the torpedo orientation along its
velocity vector, it is inaccurate but nice looking; the life span
control and the self destruction sequence; the engine acceleration
handling. When a torpedo is just fired, its engine acceleration is
huge then it decreases quickly.

With the System Browser pointed to the @method{Torpedo>>update:}
method, observe the @option{inheritance} button, it is green light, it
indicates the message is sent to @smalltalk{super} too. It is a
reminder it is a specialized behavior. The button tool tip explains
the other coloring meanings. When pressing this button, you browse the
implementations of the @method{upate:} method.

@figure{Update's inheritance button,ch06-20-TorpedoUpdateInheritance,9}

We already met an example of overriding when initializing a space ship
instance -- see @ref{initSpaceShip}. In the context of our class
hierarchy refactoring, the initialize overriding is refactored to span
over the whole @class{Mobile} hierarchy:

@smalltalkExampleCaption{Initialize overriding in the @class{Mobile}
hierarchy,initMobileHierarchy,Mobile>>initialize
   super initialize.
   color @assign{} Color gray.
   velocity @assign{} 0 @@ 0.
   acceleration @assign{} 0

SpaceShip>>initialize
   super initialize.
   self resupply

Torpedo>>initialize
   super initialize.
   lifeSpan @assign{} 500.
   acceleration @assign{} 4000}

Observe how each class is only responsible of its specific state
initialization:

@enumerate

 @item @strong{SpaceShip.} Its mechanic states is set with the
 @smalltalk{super initialize}; the ship is resupplied with fuel and torpedo:

 @smalltalkExample{SpaceShip>>resupply
   fuel @assign{} 500.
   torpedoes @assign{} 20}

 @item @strong{Torpedo.} Mechanic states initialized; self destroy
 sequence initialized and acceleration adjusted to mimic the torpedo
 boost at fire up.

@end enumerate

The behaviors specific to each mobile is set with additional
methods. The @class{SpaceShip} comes with its control methods we
already described previously in @ref{shipControls} and
@ref{fireTorpedo}, of course there is none for a
@class{Torpedo}.

Another important specific behavior is how each kind of @class{Mobile}
is drawn in the game play, this will be discussed in a next chapter on
the fundamentals of Morph.

@c refactoring central star, no need for mass only a behavior
